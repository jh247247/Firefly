task wrapper(type: Wrapper) {
  gradleVersion = '2.11'
}

buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:1.5.0'
  }
}
apply plugin: 'android'

def gitSha = 'git rev-parse --short HEAD'.execute([],project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm:ss'Z'",TimeZone.getTimeZone("UTC"))


android {
  compileSdkVersion 23
  buildToolsVersion "23"

  defaultConfig {
    buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
    buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""

    minSdkVersion 11
    targetSdkVersion 23
    versionCode 1
    versionName "1.0"
  }

  buildTypes {
    release {
      debuggable false
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
    }

    debug {
      debuggable true
      applicationIdSuffix ".debug"
      versionNameSuffix "-debug"
    }
  }

  lintOptions {
    disable 'InvalidPackage'
    abortOnError false
  }

  dexOptions {
    preDexLibraries = true
  }

}

allprojects {
  buildDir = "/tmp/${rootProject.name}/${project.name}"
}

// libraries go here!
repositories {
  mavenCentral()
  jcenter()
  maven { url "https://jitpack.io" }
}

dependencies {
  compile 'de.greenrobot:eventbus:2.4.0'

  // material dialogs
  compile('com.github.afollestad.material-dialogs:core:0.8.5.1@aar') {
    transitive = true
  }

  // Butterknife for view injection
  compile 'com.jakewharton:butterknife:7.0.1'

  // Support Libraries
  compile 'com.android.support:appcompat-v7:23.0.0'
  compile 'com.android.support:support-annotations:23.0.0'
  compile 'com.android.support:support-v4:23.0.0'

  compile 'com.squareup.retrofit:retrofit:2.0.0-beta2'
  compile 'com.squareup.retrofit:converter-gson:2.0.0-beta2'

  compile 'com.android.support:design:23.0.1'

  compile fileTree(dir: 'libs', include: ['*.jar'])
}